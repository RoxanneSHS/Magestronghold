If
	Global("psSpellUp", "LOCALS", 0)
Then
	Response #100
		ReallyForceSpell(myself, WIZARD_MIRROR_IMAGE)
		ReallyForceSpell(myself, WIZARD_STONE_SKIN)
		ReallyForceSpell(myself, WIZARD_GLOBE_OF_INVULNERABILITY)
		ReallyForceSpell(myself, WIZARD_SPELL_TRAP)
		ReallyForceSpell(myself, WIZARD_FIRE_SHIELD_RED)
		ReallyForceSpell(myself, WIZARD_ABSOLUTE_IMMUNITY)
		ReallyForceSpell(myself, WIZARD_TRUE_SIGHT)
		ReallyForceSpell(myself, WIZARD_TELEPORT_FIELD)
		SetGlobalTimer("psTimeToNextProtCast", "LOCALS", 60)
		SetGlobal("psSpellUp", "LOCALS", 1)
End

If
	RandomNum(6, 1)
		GlobalLT("psVecnaProtectionCounter", "LOCALS", 8)
		GlobalTimerExpired("psTimeToNextProtCast", "LOCALS")
Then
	Response #100
		SetGlobalTimer("psTimeToNextProtCast", "LOCALS", 60)
		ForceSpell(Myself, WIZARD_ABSOLUTE_IMMUNITY)
		ForceSpell(Myself, WIZARD_STONE_SKIN)
End

If
	RandomNum(10, 1)
	GlobalLT("psVecnaTimestopCounter", "LOCALS", 7)
	GlobalTimerExpired("psVecnaTimestopTimer", "LOCALS")
Then
	Response #100
		SetGlobalTimer("psVecnaTimestopTimer", "LOCALS", 18)
		ForceSpell(myself, WIZARD_TIME_STOP)
		IncrementGlobal("psVecnaTimestopCounter", "LOCALS", 1)
		Continue()
End


If
	RandomNum(5, 1)
	GlobalLT("psVecnaWiltingCounter", "LOCALS", 12)
Then
	Response #100
		ForceSpell(NearestEnemyOf(Myself), WIZARD_ABI_DALZIMS_HORRID_WILTING)
		IncrementGlobal("psVecnaWiltingCounter", "LOCALS", 1)
End

If
	RandomNum(12, 1)
	GlobalLT("psVecnaPlanatarCounter", "LOCALS", 3)
Then
	Response #100
		ForceSpell(NearestEnemyOf(Myself), WIZARD_SUMMON_PLANATAR_EVIL)
		IncrementGlobal("psVecnaPlanatarCounter", "LOCALS", 1)
End

If
	RandomNum(15, 1)
	GlobalLT("psVecnaSpellstrikeCounter", "LOCALS", 3)
Then
	Response #100
		ReallyForceSpell(Player1, WIZARD_SPELL_STRIKE)
		ReallyForceSpell(Player2, WIZARD_SPELL_STRIKE)
		ReallyForceSpell(Player3, WIZARD_SPELL_STRIKE)
		ReallyForceSpell(Player4, WIZARD_SPELL_STRIKE)
		ReallyForceSpell(Player5, WIZARD_SPELL_STRIKE)
		ReallyForceSpell(Player6, WIZARD_SPELL_STRIKE)
		IncrementGlobal("psVecnaSpellstrikeCounter", "LOCALS", 1)
End

If
	RandomNum(10, 1)
	GlobalLT("psVecnaDispelCounter", "LOCALS", 4)
Then
	Response #100
		ReallyForceSpell(Player1, WIZARD_TRUE_DISPEL_MAGIC)
		ReallyForceSpell(Player2, WIZARD_TRUE_DISPEL_MAGIC)
		ReallyForceSpell(Player3, WIZARD_TRUE_DISPEL_MAGIC)
		ReallyForceSpell(Player4, WIZARD_TRUE_DISPEL_MAGIC)
		ReallyForceSpell(Player5, WIZARD_TRUE_DISPEL_MAGIC)
		ReallyForceSpell(Player6, WIZARD_TRUE_DISPEL_MAGIC)		
		IncrementGlobal("psVecnaDispelCounter", "LOCALS", 1)
End

If
	RandomNum(6, 1)
	GlobalLT("psVecnaBigbysCounter", "LOCALS", 6)
Then
	Response #100
		ForceSpell(NearestEnemyOf(Myself), WIZARD_BIGBYS_CRUSHING_HAND)
		IncrementGlobal("psVecnaBigbysCounter", "LOCALS", 1)
End

If
	RandomNum(4, 1)
	GlobalLT("psSpellTriggerCounter", "LOCALS", 5)
Then
	Response #100
		ReallyForceSpell(NearestEnemyOf(Myself), WIZARD_GREATER_MALISON)
		ReallyForceSpell(NearestEnemyOf(Myself), WIZARD_CHAIN_LIGHTNING)
		ReallyForceSpell(NearestEnemyOf(Myself), WIZARD_CHAOS)
		IncrementGlobal("psSpellTriggerCounter", "LOCALS", 1)
End

If
	RandomNum(10, 1)
Then
	Response #100
		ForceSpell(NearestEnemyOf(Myself), WIZARD_MAZE)
End

If
	RandomNum(5, 1)
Then
	Response #100
		ForceSpell(NearestEnemyOf(Myself), WIZARD_FINGER_OF_DEATH)
End

If
	RandomNum(5, 1)
Then
	Response #100
		ForceSpell(NearestEnemyOf(Myself), WIZARD_SUN_FIRE)
End

If
	RandomNum(2, 1)
Then
	Response #100
		ForceSpell(NearestEnemyOf(Myself), WIZARD_FLAME_ARROW)
End

If
	RandomNum(2, 1)
Then
	Response #100
		MoveToObject(Player1)
		Continue()
End